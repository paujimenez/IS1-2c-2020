!classDefinition: #NumeroTest category: 'Numero-Exercise'!
TestCase subclass: #NumeroTest
	instanceVariableNames: 'zero one two four oneFifth oneHalf five twoFifth twoTwentyfifth fiveHalfs three eight negativeOne negativeTwo'
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Numero-Exercise'!

!NumeroTest methodsFor: 'tests' stamp: 'HernanWilkinson 5/7/2016 20:11'!
test01isCeroReturnsTrueWhenAskToZero

	self assert: zero isZero! !

!NumeroTest methodsFor: 'tests' stamp: 'HernanWilkinson 5/7/2016 20:12'!
test02isCeroReturnsFalseWhenAskToOthersButZero

	self deny: one isZero! !

!NumeroTest methodsFor: 'tests' stamp: 'HernanWilkinson 5/7/2016 20:13'!
test03isOneReturnsTrueWhenAskToOne

	self assert: one isOne! !

!NumeroTest methodsFor: 'tests' stamp: 'HernanWilkinson 5/7/2016 20:13'!
test04isOneReturnsFalseWhenAskToOtherThanOne

	self deny: zero isOne! !

!NumeroTest methodsFor: 'tests' stamp: 'HernanWilkinson 5/7/2016 20:14'!
test05EnteroAddsWithEnteroCorrectly

	self assert: one + one equals: two! !

!NumeroTest methodsFor: 'tests' stamp: 'HernanWilkinson 5/7/2016 20:18'!
test06EnteroMultipliesWithEnteroCorrectly

	self assert: two * two equals: four! !

!NumeroTest methodsFor: 'tests' stamp: 'HernanWilkinson 5/7/2016 20:20'!
test07EnteroDividesEnteroCorrectly

	self assert: two / two equals: one! !

!NumeroTest methodsFor: 'tests' stamp: 'HernanWilkinson 5/7/2016 20:38'!
test08FraccionAddsWithFraccionCorrectly
"
    La suma de fracciones es:
	 
	a/b + c/d = (a.d + c.b) / (b.d)
	 
	SI ESTAN PENSANDO EN LA REDUCCION DE FRACCIONES NO SE PREOCUPEN!!
	TODAVIA NO SE ESTA TESTEANDO ESE CASO
"
	| sevenTenths |

	sevenTenths := (Entero with: 7) / (Entero with: 10).

	self assert: oneFifth + oneHalf equals: sevenTenths! !

!NumeroTest methodsFor: 'tests' stamp: 'HernanWilkinson 5/7/2016 20:52'!
test09FraccionMultipliesWithFraccionCorrectly
"
    La multiplicacion de fracciones es:
	 
	(a/b) * (c/d) = (a.c) / (b.d)
"

	self assert: oneFifth * twoFifth equals: twoTwentyfifth! !

!NumeroTest methodsFor: 'tests' stamp: 'HernanWilkinson 5/7/2016 20:56'!
test10FraccionDividesFraccionCorrectly
"
    La division de fracciones es:
	 
	(a/b) / (c/d) = (a.d) / (b.c)
"

	self assert: oneHalf / oneFifth equals: fiveHalfs! !

!NumeroTest methodsFor: 'tests' stamp: 'HernanWilkinson 5/7/2016 21:07'!
test11EnteroAddsFraccionCorrectly
"
	Ahora empieza la diversion!!
"

	self assert: one + oneFifth equals: (Entero with: 6) / (Entero with: 5)! !

!NumeroTest methodsFor: 'tests' stamp: 'HernanWilkinson 5/7/2016 21:07'!
test12FraccionAddsEnteroCorrectly

	self assert: oneFifth + one equals: (Entero with: 6) / (Entero with: 5)! !

!NumeroTest methodsFor: 'tests' stamp: 'HernanWilkinson 5/7/2016 21:50'!
test13EnteroMultipliesFraccionCorrectly

	self assert: two * oneFifth equals: twoFifth ! !

!NumeroTest methodsFor: 'tests' stamp: 'HernanWilkinson 5/7/2016 21:52'!
test14FraccionMultipliesEnteroCorrectly

	self assert: oneFifth * two equals: twoFifth ! !

!NumeroTest methodsFor: 'tests' stamp: 'HernanWilkinson 5/7/2016 21:57'!
test15EnteroDividesFraccionCorrectly

	self assert: one / twoFifth equals: fiveHalfs  ! !

!NumeroTest methodsFor: 'tests' stamp: 'HernanWilkinson 5/7/2016 21:59'!
test16FraccionDividesEnteroCorrectly

	self assert: twoFifth / five equals: twoTwentyfifth ! !

!NumeroTest methodsFor: 'tests' stamp: 'HernanWilkinson 5/7/2016 22:38'!
test17AFraccionCanBeEqualToAnEntero

	self assert: two equals: four / two! !

!NumeroTest methodsFor: 'tests' stamp: 'HernanWilkinson 5/7/2016 22:39'!
test18AparentFraccionesAreEqual

	self assert: oneHalf equals: two / four! !

!NumeroTest methodsFor: 'tests' stamp: 'HernanWilkinson 5/7/2016 22:40'!
test19AddingFraccionesCanReturnAnEntero

	self assert: oneHalf + oneHalf equals: one! !

!NumeroTest methodsFor: 'tests' stamp: 'HernanWilkinson 5/7/2016 22:42'!
test20MultiplyingFraccionesCanReturnAnEntero

	self assert: (two/five) * (five/two) equals: one! !

!NumeroTest methodsFor: 'tests' stamp: 'HernanWilkinson 5/7/2016 22:42'!
test21DividingFraccionesCanReturnAnEntero

	self assert: oneHalf / oneHalf equals: one! !

!NumeroTest methodsFor: 'tests' stamp: 'HernanWilkinson 5/7/2016 22:43'!
test22DividingEnterosCanReturnAFraccion

	self assert: two / four equals: oneHalf! !

!NumeroTest methodsFor: 'tests' stamp: 'HernanWilkinson 5/7/2016 22:46'!
test23CanNotDivideEnteroByZero

	self 
		should: [ one / zero ]
		raise: Error
		withExceptionDo: [ :anError | self assert: anError messageText equals: Numero canNotDivideByZeroErrorDescription ]
	! !

!NumeroTest methodsFor: 'tests' stamp: 'HernanWilkinson 5/7/2016 22:46'!
test24CanNotDivideFraccionByZero

	self 
		should: [ oneHalf / zero ]
		raise: Error
		withExceptionDo: [ :anError | self assert: anError messageText equals: Numero canNotDivideByZeroErrorDescription ]
	! !

!NumeroTest methodsFor: 'tests' stamp: 'HernanWilkinson 5/7/2016 22:50'!
test25AFraccionCanNotBeZero

	self deny: oneHalf isZero! !

!NumeroTest methodsFor: 'tests' stamp: 'HernanWilkinson 5/7/2016 22:50'!
test26AFraccionCanNotBeOne

	self deny: oneHalf isOne! !

!NumeroTest methodsFor: 'tests' stamp: 'NR 5/7/2020 17:46:14'!
test27EnteroSubstractsEnteroCorrectly

	self assert: three - one equals: two! !

!NumeroTest methodsFor: 'tests' stamp: 'NR 9/23/2018 23:47:41'!
test28FraccionSubstractsFraccionCorrectly
	
	self assert: twoFifth - oneFifth equals: oneFifth.! !

!NumeroTest methodsFor: 'tests' stamp: 'NR 9/23/2018 23:48:00'!
test29EnteroSubstractsFraccionCorrectly

	self assert: one - oneHalf equals: oneHalf! !

!NumeroTest methodsFor: 'tests' stamp: 'HAW 9/24/2018 08:48:05'!
test30FraccionSubstractsEnteroCorrectly

	| sixFifth |
	
	sixFifth := (Entero with: 6) / (Entero with: 5).
	
	self assert: sixFifth - one equals: oneFifth! !

!NumeroTest methodsFor: 'tests' stamp: 'HAW 9/24/2018 08:48:08'!
test31SubstractingFraccionesCanReturnAnEntero

	| threeHalfs |
	
	threeHalfs := (Entero with: 3) / (Entero with: 2).
	
	self assert: threeHalfs - oneHalf equals: one.! !

!NumeroTest methodsFor: 'tests' stamp: 'NR 9/23/2018 23:48:48'!
test32SubstractingSameEnterosReturnsZero

	self assert: one - one equals: zero.! !

!NumeroTest methodsFor: 'tests' stamp: 'NR 9/23/2018 23:49:01'!
test33SubstractingSameFraccionesReturnsZero

	self assert: oneHalf - oneHalf equals: zero.! !

!NumeroTest methodsFor: 'tests' stamp: 'HAW 9/24/2018 08:48:14'!
test34SubstractingAHigherValueToANumberReturnsANegativeNumber

	| negativeThreeHalfs |
	
	negativeThreeHalfs := (Entero with: -3) / (Entero with: 2).	

	self assert: one - fiveHalfs equals: negativeThreeHalfs.! !

!NumeroTest methodsFor: 'tests' stamp: 'NR 9/23/2018 23:49:23'!
test35FibonacciZeroIsOne

	self assert: zero fibonacci equals: one! !

!NumeroTest methodsFor: 'tests' stamp: 'NR 9/23/2018 23:49:32'!
test36FibonacciOneIsOne

	self assert: one fibonacci equals: one! !

!NumeroTest methodsFor: 'tests' stamp: 'NR 9/23/2018 23:49:39'!
test37FibonacciEnteroReturnsAddingPreviousTwoFibonacciEnteros

	self assert: four fibonacci equals: five.
	self assert: three fibonacci equals: three. 
	self assert: five fibonacci equals: four fibonacci + three fibonacci.! !

!NumeroTest methodsFor: 'tests' stamp: 'NR 9/23/2018 23:49:47'!
test38FibonacciNotDefinedForNegativeNumbers

	self 
		should: [negativeOne fibonacci]
		raise: Error
		withExceptionDo: [ :anError | self assert: anError messageText equals: Entero negativeFibonacciErrorDescription ].! !

!NumeroTest methodsFor: 'tests' stamp: 'NR 9/23/2018 23:49:55'!
test39NegationOfEnteroIsCorrect

	self assert: two negated equals: negativeTwo.
		! !

!NumeroTest methodsFor: 'tests' stamp: 'NR 9/23/2018 23:50:03'!
test40NegationOfFraccionIsCorrect

	self assert: oneHalf negated equals: negativeOne / two.! !

!NumeroTest methodsFor: 'tests' stamp: 'NR 9/23/2018 23:50:11'!
test41SignIsCorrectlyAssignedToFractionWithTwoNegatives

	self assert: oneHalf equals: (negativeOne / negativeTwo)! !

!NumeroTest methodsFor: 'tests' stamp: 'NR 9/23/2018 23:50:17'!
test42SignIsCorrectlyAssignedToFractionWithNegativeDivisor

	self assert: oneHalf negated equals: (one / negativeTwo)! !


!NumeroTest methodsFor: 'setup' stamp: 'NR 9/23/2018 23:46:28'!
setUp

	zero := Entero with: 0.
	one := Entero with: 1.
	two := Entero with: 2.
	three:= Entero with: 3.
	four := Entero with: 4.
	five := Entero with: 5.
	eight := Entero with: 8.
	negativeOne := Entero with: -1.
	negativeTwo := Entero with: -2.
	
	oneHalf := one / two.
	oneFifth := one / five.
	twoFifth := two / five.
	twoTwentyfifth := two / (Entero with: 25).
	fiveHalfs := five / two.
	! !


!classDefinition: #Numero category: 'Numero-Exercise'!
Object subclass: #Numero
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Numero-Exercise'!

!Numero methodsFor: 'arithmetic operations' stamp: 'HernanWilkinson 5/7/2016 22:48'!
* aMultiplier

	self subclassResponsibility ! !

!Numero methodsFor: 'arithmetic operations' stamp: 'HernanWilkinson 5/7/2016 22:49'!
+ anAdder

	self subclassResponsibility ! !

!Numero methodsFor: 'arithmetic operations' stamp: 'NR 9/23/2018 22:21:28'!
- aSubtrahend

	self subclassResponsibility ! !

!Numero methodsFor: 'arithmetic operations' stamp: 'HernanWilkinson 5/7/2016 22:49'!
/ aDivisor

	self subclassResponsibility ! !

!Numero methodsFor: 'arithmetic operations' stamp: 'HernanWilkinson 5/7/2016 22:48'!
invalidNumberType

	self error: self class invalidNumberTypeErrorDescription! !

!Numero methodsFor: 'arithmetic operations' stamp: 'NR 9/23/2018 23:37:13'!
negated
	
	^self * (Entero with: -1)! !


!Numero methodsFor: 'testing' stamp: 'NR 9/23/2018 23:36:49'!
isNegative

	self subclassResponsibility ! !

!Numero methodsFor: 'testing' stamp: 'HernanWilkinson 5/7/2016 22:49'!
isOne

	self subclassResponsibility ! !

!Numero methodsFor: 'testing' stamp: 'HernanWilkinson 5/7/2016 22:49'!
isZero

	self subclassResponsibility ! !


!Numero methodsFor: 'private arithmetic operations' stamp: 'PJ 9/29/2020 19:59:42'!
addAFraccion: aFraccionAdder

	self subclassResponsibility ! !

!Numero methodsFor: 'private arithmetic operations' stamp: 'PJ 9/28/2020 21:46:02'!
addAnEntero: anEnteroAdder 

	self subclassResponsibility ! !

!Numero methodsFor: 'private arithmetic operations' stamp: 'PJ 9/29/2020 21:02:42'!
divideAFraccion: aFraccionDividend

	self subclassResponsibility ! !

!Numero methodsFor: 'private arithmetic operations' stamp: 'PJ 9/29/2020 21:02:21'!
divideAnEntero: anEnteroDividend

	self subclassResponsibility ! !

!Numero methodsFor: 'private arithmetic operations' stamp: 'PJ 9/29/2020 20:07:36'!
multiplyAFraccion: aFraccionMultiplier

	self subclassResponsibility ! !

!Numero methodsFor: 'private arithmetic operations' stamp: 'PJ 9/29/2020 20:07:15'!
multiplyAnEntero: anEnteroMultiplier 

	self subclassResponsibility ! !

!Numero methodsFor: 'private arithmetic operations' stamp: 'PJ 9/29/2020 20:56:32'!
subtractFromAFraccion: aFraccionSubtrahend

	self subclassResponsibility ! !

!Numero methodsFor: 'private arithmetic operations' stamp: 'PJ 9/29/2020 20:56:38'!
subtractFromAnEntero: anEnteroSubtrahend 

	self subclassResponsibility ! !

"-- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- "!

!classDefinition: 'Numero class' category: 'Numero-Exercise'!
Numero class
	instanceVariableNames: ''!

!Numero class methodsFor: 'error descriptions' stamp: 'NR 9/24/2020 17:28:15'!
canNotDivideByZeroErrorDescription

	^'No se puede dividir por cero!!'! !

!Numero class methodsFor: 'error descriptions' stamp: 'NR 9/24/2020 04:33:41'!
invalidNumberTypeErrorDescription
	
	^ 'Tipo de número inválido!!'! !


!classDefinition: #Entero category: 'Numero-Exercise'!
Numero subclass: #Entero
	instanceVariableNames: 'value'
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Numero-Exercise'!

!Entero methodsFor: 'arithmetic operations' stamp: 'PJ 9/29/2020 20:18:05'!
* aMultiplier 
	
	^aMultiplier multiplyAnEntero: self
	! !

!Entero methodsFor: 'arithmetic operations' stamp: 'PJ 9/29/2020 19:59:01'!
+ anAdder 
	
	^anAdder addAnEntero: self.
	
! !

!Entero methodsFor: 'arithmetic operations' stamp: 'PJ 9/29/2020 20:41:40'!
- aSubtrahend
	
	^aSubtrahend subtractFromAnEntero: self! !

!Entero methodsFor: 'arithmetic operations' stamp: 'PJ 9/29/2020 21:08:01'!
/ aDivisor
	
	^aDivisor divideAnEntero: self  ! !

!Entero methodsFor: 'arithmetic operations' stamp: 'HernanWilkinson 5/7/2016 21:55'!
// aDivisor 
	
	^self class with: value // aDivisor integerValue! !

!Entero methodsFor: 'arithmetic operations' stamp: 'PGTA 10/1/2020 05:22:36'!
fibonacci

	| one two |
	
	one := Entero with: 1.
	two := Entero with: 2.
	
	^ (self - one) fibonacci + (self - two) fibonacci
		! !

!Entero methodsFor: 'arithmetic operations' stamp: 'HernanWilkinson 5/7/2016 21:00'!
greatestCommonDivisorWith: anEntero 
	
	^self class with: (value gcd: anEntero integerValue)! !


!Entero methodsFor: 'comparing' stamp: 'HernanWilkinson 5/7/2016 21:01'!
= anObject

	^(anObject isKindOf: self class) and: [ value = anObject integerValue ]! !

!Entero methodsFor: 'comparing' stamp: 'HernanWilkinson 5/7/2016 20:17'!
hash

	^value hash! !


!Entero methodsFor: 'initialization' stamp: 'PGTA 10/1/2020 01:51:56'!
initalizeWith: aValue 
	
	value := aValue! !


!Entero methodsFor: 'value' stamp: 'HernanWilkinson 5/7/2016 21:02'!
integerValue

	"Usamos integerValue en vez de value para que no haya problemas con el mensaje value implementado en Object - Hernan"
	
	^value! !


!Entero methodsFor: 'printing' stamp: 'HAW 9/24/2018 08:53:19'!
printOn: aStream

	aStream print: value ! !


!Entero methodsFor: 'testing' stamp: 'NR 9/23/2018 22:17:55'!
isNegative
	
	^value < 0! !

!Entero methodsFor: 'testing' stamp: 'HernanWilkinson 5/7/2016 20:14'!
isOne
	
	^value = 1! !

!Entero methodsFor: 'testing' stamp: 'HernanWilkinson 5/7/2016 20:12'!
isZero
	
	^value = 0! !


!Entero methodsFor: 'private arithmetic operations' stamp: 'PJ 9/29/2020 19:59:28'!
addAFraccion: aFraccionAdder

	^self * aFraccionAdder denominator + aFraccionAdder numerator / aFraccionAdder denominator.
! !

!Entero methodsFor: 'private arithmetic operations' stamp: 'PJ 9/28/2020 21:34:16'!
addAnEntero: anEnteroAdder 
	
	^self class with: anEnteroAdder integerValue + value.
	
! !

!Entero methodsFor: 'private arithmetic operations' stamp: 'PGTA 10/1/2020 03:41:42'!
divideACero: aCero
	
	^aCero
	
! !

!Entero methodsFor: 'private arithmetic operations' stamp: 'PJ 9/29/2020 21:29:31'!
divideAFraccion: aFraccionDividend
	
	^aFraccionDividend numerator / (aFraccionDividend denominator * self)
	
! !

!Entero methodsFor: 'private arithmetic operations' stamp: 'PGTA 10/1/2020 05:14:32'!
divideAnEntero: anEnteroDividend

	^Fraccion with: anEnteroDividend over: self! !

!Entero methodsFor: 'private arithmetic operations' stamp: 'PJ 9/29/2020 20:16:34'!
multiplyAFraccion: aFraccionMultiplier
	
	^self * aFraccionMultiplier numerator / aFraccionMultiplier denominator
	
! !

!Entero methodsFor: 'private arithmetic operations' stamp: 'PJ 9/29/2020 20:10:58'!
multiplyAnEntero: anEnteroMultiplier 
	
	^self class with: value * anEnteroMultiplier integerValue
	
! !

!Entero methodsFor: 'private arithmetic operations' stamp: 'PJ 9/29/2020 20:49:17'!
subtractFromAFraccion: aFraccionMinuend
	
	
	| newNumerator newDenominator |
	
	newNumerator := aFraccionMinuend numerator - (self * aFraccionMinuend denominator).
	newDenominator := aFraccionMinuend denominator.
	
	^newNumerator / newDenominator
! !

!Entero methodsFor: 'private arithmetic operations' stamp: 'PJ 9/29/2020 20:40:50'!
subtractFromAnEntero: anEnteroMinuend
	
	^ self class with: anEnteroMinuend integerValue - value
	
! !

"-- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- "!

!classDefinition: 'Entero class' category: 'Numero-Exercise'!
Entero class
	instanceVariableNames: ''!

!Entero class methodsFor: 'instance creation' stamp: 'NR 4/8/2019 02:57:57'!
negativeFibonacciErrorDescription
	^ ' Fibonacci no está definido aquí para Enteros Negativos'! !

!Entero class methodsFor: 'instance creation' stamp: 'PGTA 10/1/2020 04:34:28'!
with: aValue 
	
	"Esta verificacion esta puesta por si se equivocan y quieren crear un Entero pasando otra cosa que un Integer - Hernan"
	aValue isInteger ifFalse: [  self error: 'aValue debe ser anInteger' ].
	
	aValue = 0 ifTrue:[^Cero new initalizeWith: aValue ].
	aValue = 1 ifTrue:[^Uno  new initalizeWith: aValue ].
	aValue < 0 ifTrue:[^Negativo  new initalizeWith: aValue].
	^self new initalizeWith: aValue! !


!classDefinition: #Cero category: 'Numero-Exercise'!
Entero subclass: #Cero
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Numero-Exercise'!

!Cero methodsFor: 'private arithmetic operations' stamp: 'PGTA 10/1/2020 03:04:19'!
divideACero: aCero
	
	"Se podría hacer un error más específico pero no me parece tan relevante para el ejercicio"
	^self error: self class canNotDivideByZeroErrorDescription.! !

!Cero methodsFor: 'private arithmetic operations' stamp: 'PGTA 10/1/2020 04:31:25'!
divideAnEntero: anEnteroDividend
	
	^Fraccion error: Fraccion canNotDivideByZeroErrorDescription.! !


!Cero methodsFor: 'arithmetic operations' stamp: 'PGTA 10/1/2020 02:57:07'!
/ aDivisor
	
	^aDivisor divideACero: self! !

!Cero methodsFor: 'arithmetic operations' stamp: 'PGTA 10/1/2020 05:21:55'!
fibonacci

	^Entero with: 1! !


!classDefinition: #Negativo category: 'Numero-Exercise'!
Entero subclass: #Negativo
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Numero-Exercise'!

!Negativo methodsFor: 'private arithmetic operations' stamp: 'PGTA 10/1/2020 04:59:25'!
divideAnEntero: anEnteroDividend 
	
	^anEnteroDividend negated / self negated! !

!Negativo methodsFor: 'private arithmetic operations' stamp: 'PGTA 10/1/2020 05:13:28'!
multiplyAnEntero: anEnteroMultiplier 
	
	^Entero with: value * anEnteroMultiplier integerValue! !


!Negativo methodsFor: 'arithmetic operations' stamp: 'PGTA 10/1/2020 05:18:31'!
fibonacci

	^self error: Entero negativeFibonacciErrorDescription! !


!classDefinition: #Uno category: 'Numero-Exercise'!
Entero subclass: #Uno
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Numero-Exercise'!

!Uno methodsFor: 'private arithmetic operations' stamp: 'PGTA 10/1/2020 02:03:30'!
addAnEntero: anEnteroValue
	
	^Entero with: anEnteroValue integerValue + value.! !

!Uno methodsFor: 'private arithmetic operations' stamp: 'PGTA 10/1/2020 02:10:05'!
subtractFromAnEntero: anEnteroMinuend
	
	^ Entero with: anEnteroMinuend integerValue - value! !


!Uno methodsFor: 'arithmetic operations' stamp: 'PGTA 10/1/2020 05:20:08'!
fibonacci

	^self
		! !


!classDefinition: #Fraccion category: 'Numero-Exercise'!
Numero subclass: #Fraccion
	instanceVariableNames: 'numerator denominator'
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Numero-Exercise'!

!Fraccion methodsFor: 'arithmetic operations' stamp: 'PJ 9/29/2020 20:21:57'!
* aMultiplier 
	
	^aMultiplier multiplyAFraccion: self! !

!Fraccion methodsFor: 'arithmetic operations' stamp: 'PJ 9/29/2020 20:05:20'!
+ anAdder 
		
	^anAdder addAFraccion: self
	
	 ! !

!Fraccion methodsFor: 'arithmetic operations' stamp: 'PJ 9/29/2020 20:44:20'!
- aSubtrahend
		
	^aSubtrahend subtractFromAFraccion: self 
	
	
! !

!Fraccion methodsFor: 'arithmetic operations' stamp: 'PJ 9/29/2020 21:13:55'!
/ aDivisor 
	
	^aDivisor divideAFraccion: self ! !


!Fraccion methodsFor: 'comparing' stamp: 'HernanWilkinson 5/7/2016 20:42'!
= anObject

	^(anObject isKindOf: self class) and: [ (numerator * anObject denominator) = (denominator * anObject numerator) ]! !

!Fraccion methodsFor: 'comparing' stamp: 'HernanWilkinson 5/7/2016 20:50'!
hash

	^(numerator hash / denominator hash) hash! !


!Fraccion methodsFor: 'accessing' stamp: 'HernanWilkinson 5/7/2016 21:56'!
denominator

	^ denominator! !

!Fraccion methodsFor: 'accessing' stamp: 'HernanWilkinson 5/7/2016 21:56'!
numerator

	^ numerator! !


!Fraccion methodsFor: 'initialization' stamp: 'HernanWilkinson 5/7/2016 22:54'!
initializeWith: aNumerator over: aDenominator

	"Estas precondiciones estan por si se comenten errores en la implementacion - Hernan"
	aNumerator isZero ifTrue: [ self error: 'una fraccion no puede ser cero' ].
	aDenominator isOne ifTrue: [ self error: 'una fraccion no puede tener denominador 1 porque sino es un entero' ].
	
	numerator := aNumerator.
	denominator := aDenominator ! !


!Fraccion methodsFor: 'testing' stamp: 'NR 9/23/2018 23:41:38'!
isNegative
	
	^numerator < 0! !

!Fraccion methodsFor: 'testing' stamp: 'HernanWilkinson 5/7/2016 22:51'!
isOne
	
	^false! !

!Fraccion methodsFor: 'testing' stamp: 'HernanWilkinson 5/7/2016 22:51'!
isZero
	
	^false! !


!Fraccion methodsFor: 'printing' stamp: 'HAW 9/24/2018 08:54:46'!
printOn: aStream

	aStream 
		print: numerator;
		nextPut: $/;
		print: denominator ! !


!Fraccion methodsFor: 'private arithmetic operations' stamp: 'PJ 9/29/2020 20:01:48'!
addAFraccion: aFraccionAdder
	
	| newNumerator newDenominator |
	
	newNumerator := (numerator * aFraccionAdder denominator) + (denominator * aFraccionAdder numerator).
	newDenominator := denominator * aFraccionAdder denominator.
	
	^newNumerator / newDenominator ! !

!Fraccion methodsFor: 'private arithmetic operations' stamp: 'PJ 9/29/2020 21:40:15'!
addAnEntero: anEnteroAdder 
	
	| newNumerator newDenominator |
	
	newNumerator := anEnteroAdder * denominator + numerator.
	newDenominator := denominator.
	
	^newNumerator / newDenominator ! !

!Fraccion methodsFor: 'private arithmetic operations' stamp: 'PJ 9/29/2020 21:32:40'!
divideAFraccion: aFraccionDividend
	
	^aFraccionDividend numerator * denominator / (aFraccionDividend denominator * numerator)! !

!Fraccion methodsFor: 'private arithmetic operations' stamp: 'PGTA 10/1/2020 03:52:28'!
divideAUno: aUno
	
	^self class with: denominator over: numerator ! !

!Fraccion methodsFor: 'private arithmetic operations' stamp: 'PJ 9/29/2020 21:07:56'!
divideAnEntero: anEnteroDividend
	
	^anEnteroDividend * denominator / numerator! !

!Fraccion methodsFor: 'private arithmetic operations' stamp: 'PJ 9/29/2020 21:42:54'!
multiplyAFraccion: aFraccionMultiplier
	
	^numerator * aFraccionMultiplier numerator / (denominator * aFraccionMultiplier denominator)! !

!Fraccion methodsFor: 'private arithmetic operations' stamp: 'PJ 9/29/2020 21:42:36'!
multiplyAnEntero: anEnteroMultiplier 
	
	^numerator * anEnteroMultiplier / denominator! !

!Fraccion methodsFor: 'private arithmetic operations' stamp: 'PJ 9/29/2020 20:54:31'!
subtractFromAFraccion: aFraccionMinuend
	
	| newNumerator newDenominator |
	
	newNumerator := (aFraccionMinuend numerator * denominator) - (aFraccionMinuend denominator * numerator).
	newDenominator := aFraccionMinuend denominator * denominator.
	
	^newNumerator / newDenominator ! !

!Fraccion methodsFor: 'private arithmetic operations' stamp: 'PJ 9/29/2020 20:49:40'!
subtractFromAnEntero: anEnteroMinuend
	
	^anEnteroMinuend * denominator - numerator / denominator ! !

"-- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- "!

!classDefinition: 'Fraccion class' category: 'Numero-Exercise'!
Fraccion class
	instanceVariableNames: ''!

!Fraccion class methodsFor: 'instance creation' stamp: 'PGTA 10/1/2020 05:15:53'!
with: aDividend over: aDivisor

	| greatestCommonDivisor numerator denominator |
	
	greatestCommonDivisor := aDividend greatestCommonDivisorWith: aDivisor. 
	numerator := aDividend // greatestCommonDivisor.
	denominator := aDivisor // greatestCommonDivisor.
	
	denominator isOne ifTrue: [ ^numerator ].

	^self new initializeWith: numerator over: denominator
	! !
